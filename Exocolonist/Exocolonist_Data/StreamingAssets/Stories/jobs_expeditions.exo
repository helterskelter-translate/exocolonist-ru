
/******************************************************************************* }*/
/** Экспедиции *************************************************************/
/******************************************************************************* {*/

=== snippet_doneboss ==============================================
	// используется в конце каждого босса
	
	* Продолжить исследование
		~ifd skill_stress < 100
		~call preventSeenSkip
		~call preventDoneCancel()
	
	* Идти домой
		~call preventSeenSkip
		~call goHome()
		
		
=== expeditions ==============================================
	~if location = none
	~call keep // нужно, когда это появляется на постоянном месте карты
	
	~set bg = expeditions
	
	// остановлен взрослым до 13 лет (и Тонин печален, потом зол/недоволен после смерти дочери)
	~setif season != glow && !mem_job_survey ? var_stopped
	~setif var_stopped ? left = tonin
	~setif var_stopped && mem_dead_tammy ? left = tonin_angry
	~setif var_stopped && mem_dead_tammy && story_main_tammyDie <= 5 ? left = tonin_sad
	~setif var_stopped && mem_chiefSurveyor == utopia || mem_dead_tonin ? left = utopia
	
	[if season = glow]
		// кликнул на закрытые ворота во время сезона Сияния, здесь ничего нет
	
		[if mem_newShip]
			Сияние — не время для исследований. Это _слишком_ опасно. Ворота закрыты, а экспедиции приостановлены, пока через несколько недель не вернется свет.
			
			[if mem_job_exploreGlow]
				Хорошо, что ты знаешь другой выход...
			[end]
		[else]
			Ты слишком напуган, чтобы выходить наружу во время Сияния. И кроме того, ворота закрыты.
		[end]
	
	[else if !mem_expeditionsUnlock]
		// нужна выносливость >= 10, чтобы покинуть колонию до любой работы по исследованию, включая скрытные вылазки
		
		Ты осторожно проходишь через ворота колонии.
		
		[if mem_chiefSurveyor != utopia && !mem_dead_tonin]
			// возраст 10 - 11, все равно слишком молод, возможны только скрытные вылазки
			[if mem_dead_tammy && story_main_tammyDie <= 5]
				// он печален после смерти Темми
				"[Name]."
				
				Дядя Тонин вздыхает, преграждая тебе путь. Он выглядит так, будто не спал. "Прости, но тебе не стоит быть здесь. Наши дети так молоды, и... и так драгоценны. Так хрупки. Пожалуйста, оставайся рядом с кораблем."
				
			[else if mem_dead_tammy]
				// он все еще выглядит уставшим до конца игры
				"Ну, привет, [Name]."
				
				Дядя Тонин пытается собрать свою старую улыбку. "Боюсь, тебе пока лучше заниматься спортболом, ладно? Снаружи этих стен мир суров."
			[else]
				"Эй, привет, малыш!"
				
				Дядя Тонин с улыбкой преграждает тебе путь. "Боюсь, экспедиции сейчас запрещены. Там снаружи небезопасно для детей. Тебе лучше стать покрепче, чтобы присоединиться к командам, когда подрастешь!"
			[end]
			
		[else]
			// возраст 12 - 19, работа инспектора открывается в 13 лет и будет доступна после прохождения проверки на выносливость
			"[Name]! Как дела, маленький [pardner|сестренка|братишка]?"
			
			Главный инспектор Утопия небрежно выходит, преграждая тебе путь, делая вид, что это случайность. "Эй, полегче! Не знаю, готов ли ты уже выходить туда и присоединяться к инспекторам. Почему бы не смыться и не заняться спортболом внутри стен пока что?"
			
			Она трет тебе костяшками пальцев макушку и отправляет восвояси.
			
		[end]
	
	[else if !mem_job_survey]
		// ты достаточно крепок, но все еще слишком молод до 13 лет
		// скрытные вылазки обрабатываются через дренажную трубу возле гарнизона, как только она будет открыта через Диса
		
		Ты осторожно проходишь через ворота колонии.
		
		[if mem_chiefSurveyor != utopia && !mem_dead_tonin]
			[if mem_dead_tammy]
				"Любопытный росточек, не так ли, [Name]?"
				
				Дядя Тонин преграждает тебе путь. Его взгляд отрешен, но он собирает для тебя неискреннюю улыбку. "Хочешь узнать больше об экспедициях? Боюсь, ты все еще слишком молод, чтобы присоединиться к командам, но скоро станешь отличным инспектором. Может, через пару лет, ладно?"
			[else]
				Дядя Тонин с хохотом останавливает тебя. Он здоровяк и преграждает тебе путь через ворота.
				
				"Хочешь посмотреть на карту или бестиарий, малыш? Я не могу отпустить тебя наружу, ты слишком молод для этого!"
			[end]
		[else]
			[if random]
				Главный инспектор Утопия останавливает тебя, положив руку тебе на лоб. "Эй, стой, проказник! Меня убьют, если я выпущу тебя из колонии. Прости, малыш, придется подождать, пока подрастешь. Знаю, это трудно."
			[else]
				Тебе не застать Главного инспектора Утопию врасплох. Она может заметить ребенка, пытающегося проскользнуть в ворота, за полкилометра. Она ухмыляется и делает пальцами жест "разворачивайся".
			[else]
				Главный инспектор Утопия просто скрещивает руки и качает головой. Ты _знаешь_, что тебе нельзя выходить наружу, но... но...
			[end]
		[end]
		
	[end]
	
	// нет места для текста после 13 лет
	
	* "Я достаточно крепок!"
		~if !call_isdemo
		~if !mem_expeditionsUnlock
		~ifd skill_toughness >= 20
		~call story(expeditionsUnlock)
	
	* "Я достаточно храбр!"
		~if !call_isdemo
		~if !mem_expeditionsUnlock
		~ifd skill_bravery >= 20
		~call story(expeditionsUnlock)
	
	* "Я достаточно крепок!"
		// более легкое требование для демо
		~if call_isdemo
		~if !mem_expeditionsUnlock
		~ifd skill_toughness >= 10
		~call story(expeditionsUnlock)
	
	* "Я достаточно храбр!"
		// более легкое требование для демо
		~if call_isdemo
		~if !mem_expeditionsUnlock
		~ifd skill_bravery >= 10
		~call story(expeditionsUnlock)
	
	* Провериться у Начальника Ретта
		~if mem_expeditionsUnlock
		~if mem_huntFacilityWon
		~if age >= 19
		~call story(huntUltimateStart)
		
	* Подать заявку на фуражира
		~if mem_expeditionsUnlock
		~if story_surveyUnlockForage
		~if !mem_job_forage
		~call story(surveyUnlockForage)
		
	* Подать заявку на охотника
		~if mem_expeditionsUnlock
		// чтобы можно было сказать "нет" навсегда и убрать эту опцию
		~if mem_jobready_hunt
		~if !mem_job_hunt
		~call story(exploreUnlockHunt)
		
	* Посмотреть Карту
		~call preventSeenSkip
		> map
	
	* Посмотреть Бестиарий
		~call preventSeenSkip
		> bestiary
	
	* Просочиться через дренажную трубу
		// Скрытная вылазка для исследования вызывается из события sneakDrainpipe
		// но игроки не могут найти ее, так что оставь и здесь
		~if mem_job_sneak
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call cheevo(general_sneak_out)
		~call job(sneak)
		
	* Просочиться Исследовать в Сияние
		// в любом возрасте нельзя выходить во время Сияния, кроме как открыто в пирамиде хребтов
		// больше не перк, потому что теоретически можно получить любой перк в 10 лет
		~if season == glow
		~if mem_job_exploreGlow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~ifd skill_bravery >= 30
		~call job(exploreGlow)
	
	* Исследовать Окрестности
		// в основном то же самое, но не называй это скрытной вылазкой после 12 лет
		~if mem_expeditionsUnlock
		~if mem_job_exploreNearby
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call job(exploreNearby)
	
	* Инспектировать Равнины
		~if mem_expeditionsUnlock
		~if mem_job_survey
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call job(survey)
	
	* Собирать в Долине
		~if mem_expeditionsUnlock
		~if mem_job_forage
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call job(forage)
	
	* Инспектировать Нагорье
		~if mem_expeditionsUnlock
		~if mem_job_artifacts
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call job(artifacts)
	
	* Охотиться на Болотах
		~if mem_expeditionsUnlock
		~if mem_job_hunt
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~ifd status != injured
		~call job(hunt)
		
	* Уйти
		// на случай, если ты каким-то образом окажешься снаружи во время Сияния или Хал преградит путь внутри
		~callif season == glow || !mem_expeditionsUnlock || !mem_job_survey ? movePlayerTo(exploreGlow)
		
	*= map
		// ~call delayText(2) // вызывается каждый раз или вообще не вызывается, не беспокойся
		~call preventSeenSkip
		~set bg = expeditions_map
		
		Инфоокно всплывает на твоей гололадони, когда ты приближаешься. О чем ты хочешь узнать?
		
		** Прозаичные Равнины
			~call preventSeenSkip
		
			Прозаичные Равнины лежат далеко к северу от колонии, где более высокое расположение и естественный наклон планеты заставляет бромный снег выпадать чаще в любое время года.
			
			Хотя Равнины подвержены внезапным снежным шквалам, это относительно безопасное для исследования место, наполненное в основном травоядной ксенофауной.
			
			>> map
		
		** Долина Головокружения
			~call preventSeenSkip
		
			Долина Головокружения — это низко расположенная траншея на востоке, густо засаженная деревьями, производящими пыльцу. Здесь есть большое разнообразие съедобной пищи — как растительной, так и животного происхождения, — что заставляет некоторых исследователей называть это место "житницей Вертумны".
			
			Здесь есть ряд животных, которых заинтересовала бы Геопоника для одомашнивания.
			
			>> map

		** Западное Нагорье
			~call preventSeenSkip
		
			Этот сложный для произношения регион — сломанный хребет планеты, дом как для головокружительных высот, так и для пугающих глубин. Здесь всегда жарко и сухо, независимо от сезона.
			
			Пересекать эти крутые расщелины опасно, и это могут делать только те, у кого есть достаточная подготовка с планерами. Однако здесь есть интересные руины для исследования.
			
			>> map
		
		** Подводное Болото
			~call preventSeenSkip
		
			К югу простирается обширная болотистая глушь, дом для опасных животных и неблагоприятных погодных условий. Исследовать этот район следует только с крайней осторожностью.
			
			>> map
			
		** Назад
			~call preventSeenSkip
			> start
		
	*= bestiary
		~set left = none
	
		** Аэрокальмар
			~if mem_xeno_airsquid
			~call preventSeenSkip
			
			_Аэрокальмар - Xenolevis cephalopoda_
			
			Крупные, неуклюжие существа с грубой серой кожей, наполненной гелиевыми мешками. Парят над линией деревьев, ловя неосторожную добычу длинными, волочащимися щупальцами.
			
			>>>

		** Бальнеал
			~if mem_xeno_balneal
			~call preventSeenSkip
			
			_Бальнеал - Xenosynth Balneal_
			
			Гигантские, проворные фильтраторы, которые также способны к фотосинтезу благодаря лохматой, похожей на траву шерсти на спинной стороне. Часто являются домом для колоний птиц, которые ждут, когда бальнеал выпустит воду из дыхала, и хватают мелких водных животных, попавших внутрь.
			
			>>>
			
		** Ласкопрыг
			~if mem_xeno_bounceweasel
			~call preventSeenSkip
			
			_Ласкопрыг - Xenomustela saltus_
			
			Агрессивные и территориальные коллективные охотники, похожие по внешнему виду на земных куньих, таких как ласка. Способны падать с большой высоты и "подпрыгивать" телами на земле.
			
			[if mem_bounceweaselRecording]
				Характерный высокий охотничий крик используется, чтобы испугать и дезориентировать их добычу.
			[end]
			
			>>>
		
		** Щетинеслизень
			~if mem_xeno_bristleslug
			~call preventSeenSkip
			~set left = bristleslug

			_Щетинеслизень - Xenosynth gastropoda_
		
			Медлительные, туповатые и спокойные, кроме брачного сезона; их шипы и ягоды полностью съедобны, как и их мясо.
			
			>>>
		
		** Кустарник
			~if mem_xeno_bushbub
			~call preventSeenSkip
		
			_Кустарник - Xenosynth arborescens_
			
			Часть животное, часть растение, часть минерал. Умные и в основном мирные хранители, они расчищают тропы в лесу, собирая обломки и другие интересные вещи для своих "святилищ".
			
			>>>		
		
		** Диллигусеница
			~if mem_xeno_dillypillar
			~call preventSeenSkip
			~set left = dillypillar
			
			_Диллигусеница - Xenosaurus polyped_
			
			[if mem_socksFood]
				Послушные, похожие на гусениц существа, которые быстро увеличиваются в длину. Полностью зрелые диллигусеницы могут вырастать до сотен метров в длину.
			[else]
				Послушные, похожие на гусениц существа, которые быстро увеличиваются в длину.
			[end]
			
			>>>
			
		** Милотылёк
			~if mem_xeno_dorbsmoth
			~call preventSeenSkip
			
			_Милотылёк - Xenomoth amicus_
			
			Послушные вредители размером с собаку с большими крыльями с голубой шерстью и усатыми усиками, как у сома. Способны протискиваться через небольшие щели.
			
			>>>
		
		** Глазезверь
			~if mem_xeno_eyebeast
			~call preventSeenSkip
			
			_Глазезверь - Xenocanid ocula_
			
			Похожие на собак существа с переливчатой маслянисто-черной кожей и множественными глазными наростами по всему телу.
			
			>>>
	
		** Безликий
			~if mem_xeno_faceless
			~call preventSeenSkip
			~set left = faceless
			
			_Безликий - Xenosynth horrificus_
			
			Химерические ужасы. Их массивные тела заканчиваются не головой, а пастью с зубами и щупальцами. Невероятно крепкие и агрессивные.
			
			>>>
		
		** Воздухорова
			~call preventSeenSkip
			~set left = floatcow
			
			_Воздухорова - Xenolevis bovidae_
			
			Эти выглядящие неуклюже существа на самом деле легче воздуха благодаря большим гелиевым мешкам на спинной и брюшной сторонах. Точный баланс гелия и мочи позволяет им контролировать высоту.
			
			Они производят соленое, богатое белком молочное вещество для кормления своих детенышей, которое также питательно для людей.
			
			>>>
			
		** Попрыглаз
			~call preventSeenSkip
			~set left = hopeye
			
			_Обычный Попрыглаз - Xenofelis Monopodus_
			
			Коллективное существо с двумя разными наборами глаз и одной сильной ногой, используемой как для передвижения, так и для защиты.
			
			Хотя и безвредны, эти маленькие воины могут быть свирепыми в группах.
			
			>>>
			
		** Лжецветок
			~if mem_xeno_liarflower
			~call preventSeenSkip
			
			_Лжецветок - Xenosynth mendacia_
			
			Хитрая охотница, замаскированная под гигантский сладко пахнущий цветок с пятью крупными лепестками.
			
			>>>
		
		** Мантикора
			~call preventSeenSkip
			~set left = manticore
			
			_Мантикора - Xenofelis Tyrannus_
			
			Высокоинтеллектуальные и хищные. Их оперение меняется в зависимости от сезона.
			
			Их тело состоит почти полностью из опасного оружия. Не приближайся.
		
			>>>
			
		** Грязеход 
			~if mem_xeno_muckwalker
			~call preventSeenSkip
			
			_Грязеход - Xenoichthys ambluae_
			
			Похожие на рыб существа, живущие в грязи. Можно наблюдать на суше среди деревьев во время Влаги, когда многие части леса затапливаются.
	
			>>>
			
		** Улей Сквиджеров
			~if mem_xeno_squeedger
			~call preventSeenSkip
			
			_Сквиджер - Xenohiveal Nestmother_
			
			Взрослые самки сквиджеров, прошедшие метаморфоз в неподвижный "улей" для следующего поколения. Выделяют сладкий "мед" из своих глубоких пор, похожих на туннели, чтобы кормить личинок, — и кислотную дымку, чтобы защищать их.
			
			>>>
		
		** Слизнехват
			~if mem_xeno_snapbladder
			~call preventSeenSkip
			
			_Слизнехват - Xenohiveal Tumescus_
			
			Желеобразные, покрытые шипами существа, которые охотятся, поджидая в засаде и нападая на добычу. Остерегайся их гнезд с детенышами, которые совсем не беспомощны, какими пытаются казаться. Сильно ядовиты.
			
			>>>
			
		** Сахарник
			~if mem_xeno_sugarbug
			~call preventSeenSkip
			
			_Обычный Сахарник - Xenohiveal Saccharum_
			
			Мелкие, похожие на шары желеобразные существа, встречающиеся большими колониями. Выделяют сладкий съедобный сироп при провоцировании.
			
			>>>
		
		** [=mem_surveyPhotosynthsName]
			~if mem_xeno_sunbaskers
			~call preventSeenSkip
			
			Твои собственные _[=mem_surveyPhotosynthsName] - Xenosynth Helianthus_!
			
			Величественные копытные с лопатоподобным прикусом и черепообразной головой. Передвигаются большими стадами и любят нежиться на солнце.
			
			>>>
		
		** Триппет
			~if mem_xeno_trippet
			~call preventSeenSkip
			~set left = trippet
			
			_Триппет - Xenosynth tripetiole_
			
			Больше растение, чем животное, но способные к небольшой подвижности и сознанию. Свистят через поры, чтобы общаться с другими.
			
			>>>
			
		** Единозавр
			~if mem_xeno_unisaur
			~call preventSeenSkip
			~set left = unisaur
			
			_Единозавр - Xenosaurus Unicorn_
			
			Обычно послушные плотоядные рептилии, чрезвычайно опасны, когда голодны. Питаются раз в две недели, используя свой внутренний глоточный рот, чтобы втягивать добычу, как угорь.
			
			>>>
			
		** Врики
			~if mem_xeno_vriki
			~call preventSeenSkip
			~set left = vriki
		
			_Врики - Xenaraneae Amicus_
			
			Подвижны только в ювенильной форме, врики — любопытное и в основном безобидное существо с симбиотическими грибковыми наростами.
			
			[if story_forageVrikiTree]
				В какой-то момент своего жизненного цикла врики укореняется и начинает медленную метаморфозу в материнское дерево, давая жизнь сотням тысяч семеподобных детенышей.
			[end]
			
			>>>
			
		** Назад
			~call preventSeenSkip
			> start
		
		
=== sneakDrainpipe ==============================================
	// прикреплено к месту-триггеру на карте возле гарнизона, пока активна job_sneak, только в страто возрасте 10-14 лет
	~if location = none
	
	~set bg = walls
	~setif season = glow ? left = dys

	[if season = glow]
		"Эй, что ты делаешь?" — говорит Дис, заметив, как ты пытаешься снять решетку с дренажной трубы, ведущей наружу стен. "Не будь пустоголовым! Разве ты не видел дополнительных дозорных на стенах? Они точно тебя заметят, потом найдут наш секретный выход, и его не станет!"
		
		Ты полагаешь, он прав. Они _действительно_ много патрулировали во время Сияния... ты не хочешь быть пойманным.
		
	[else if status = grounded]
		Ты попадешь в _тройные_ неприятности, если тебя поймают на скрытной вылазке, пока ты наказан. Даже _четверные_. Лучше не рисковать.
	
	[else]
		Ты видел, как Дис использует эту дренажную трубу за гарнизоном, чтобы проскользнуть сквозь стены. Там есть металлическая решетка, которую можно снять, чтобы проползти.
		
		Ты хочешь выйти туда?
	[end]

	* Просочиться Исследововать Окрестности
		// называй это скрытной вылазкой до 12 лет
		~if mem_job_sneak
		~ifd season != glow
		~ifd status != stressed
		~ifd status != grounded
		~ifd status != mourning
		~call cheevo(general_sneak_out)
		~call job(sneak)
	
	* Уйти
		~call forceDoneCancel
		// пусто


=== sneakDrainpipeReturn ==============================================
	// прикреплено к месту-триггеру на карте NearbyStrato
	~if location = none
	
	~set bg = walls
	
	Дренажная труба в стене все еще открыта, и никого нет рядом.
	
	Хочешь проползти обратно?
	
	* Идти домой
		Ты уверен? Это завершит месяц.
			
		** Нет, продолжать исследование
			~call preventSeenSkip
			~call forceDoneCancel
			// конец
		
		** Да, идти домой
			~call preventSeenSkip
			~call goHome()
		
	* Продолжить исследование
		~call preventSeenSkip
		~call forceDoneCancel
		// конец
		
		
=== home ==============================================
	~if location = none
	// на автобусной станции в начале карт экспедиций, включая окрестности, ведет домой
	
	~set bg = expeditionsReturn
	
	[if job = sneak]
		Ты пробираешься к офису Экспедиций у ворот колонии. Никто тебя не видел... _пока_. Оживленная суета через ворота как-то заставляет скучать по друзьям внутри.
		
		Если хочешь домой, можно сейчас проскользнуть обратно через ту дренажную трубу.
	
	[else if biome = nearby]
		Ты подходишь к воротам колонии.
	
	[else]
		Эта небольшая беседка обустроена как зона ожидания для автоматических транспортеров, которые снуют туда-сюда отсюда до колонии. На знаке написано:
		
		[if job = survey]
		
			"_Автобусная станция: Прозаичные Равнины_"
			
			// Активность ксенофауны: низкая
			// Опасности окружающей среды: ксенофауна в круглых бассейнах
			// Местные ресурсы: грибодрево, искрящийся снег
			
			Скудная растительность делает этот регион идеальным для добычи ресурсов.
			
			Голубой "снег" слабокислотный и может разъедать оборудование при длительном воздействии. Пожалуйста, промой защитные скафандры по возвращении.
			
			
		[else if job = forage]
		
			"_Автобусная станция: Долина Головокружения_"
			
			// Активность ксенофауны: средняя
			// Опасности окружающей среды: пыльца ксинофлоры
			// Местные ресурсы: фрукты, ботанические образцы
			
			Внимание! Воздушные фильтры обязательны в этой зоне.
			
			Фруктов в изобилии, но, пожалуйста, идентифицируйте вид перед сбором, так как некоторые виды могут яростно возражать.
			
			Ксенофауна может вести себя более агрессивно в этой области. Всегда приближайся к диким животным с осторожностью.
			
			
		[else if job = artifacts]
		
			"_Автобусная станция: Западное Нагорье_"
			
			// Активность ксенофауны: средняя
			// Опасности окружающей среды: края обрывов, обезвоживание
			// Местные ресурсы: минералы
			
			Водосборный дрон и 4 литра воды в день требуются в этой зоне.
			
			Если ты испытываешь головокружение, галлюцинации или потерю времени, пожалуйста, сообщи о местоположении для дальнейшего изучения.
			
		[else if job = hunt]
		
			"_Автобусная станция: Подводное Болото_"
			
			// Активность ксенофауны: высокая
			// Опасности окружающей среды: ксенофауна, электрические бури
			// Местные ресурсы: грибодрево, продукты животного происхождения
			
			Внимание! Опасная ксенофауна в этой области. Рекомендуется защитное снаряжение. Действуйте с осторожностью возле воды.
			
			Электрические разряды часты во время бурь. Регулярно проверяй систему заземления своего защитного скафандра!
			
		[end]
	[end]
	
	* Идти домой
	
		Ты уверен? Это завершит месяц.
	
		** Нет, продолжать исследование
			~call preventSeenSkip
			~call forceDoneCancel
			// конец
		
		** Да, идти домой
			~call preventSeenSkip
			~call goHome()
		
	* Продолжить исследование
		~call preventSeenSkip
		~call forceDoneCancel
		// конец


=== bossReturnFlag ==============================================
	// прикреплено к пустым местам боссов после частичного обновления, когда они очищены
	~if location = none
	
	~set bg = explore
	~set sprite = flag
	
	Ты достиг конца этой области...
	
	* Продолжить исследование
		~ifd skill_stress < 100
		// чтобы избежать +15 стресса
		~call preventSeenSkip
		~call keep 
	
	* Идти домой
		~call preventSeenSkip
		~call goHome()
		
		
		
=== exploreStressMax ==============================================
	// может быть вызван только напрямую
	~if location = none
	
	~set bg = explore
	
	[if status = injured]
		Ты слишком травмирован, чтобы продолжать! Тебе пора домой...
	
	[else]
		Ты слишком измотан и устал, чтобы продолжать! Тебе пора домой...
	
	[end]
	
	* Идти домой
		~call goHome()
	
			
=== sneakEnd ==============================================
	~if job = high_sneak
	// только дважды, вероятно, во 2-й и 4-й раз, когда просачиваешься
	~if mem_work_sneak > 0
	~if mem_work_sneak != 2
	~if mem_sneakEnd < 2
	~if !mem_dead_tonin
	// если только Утопия не поймала тебя или ты не ночевал на улице, или когда-либо спасал Тонина
	~if status != grounded
	~if story_nearbyBossDysCamp != 0
	~if !story_nearbyBossToninDeath
	
	~set mem_sneakEnd++
	~set bg = sportsball
	
	Ты проползаешь через большую дренажную трубу за спортбольным полем, осторожно сдвигая металлическую решетку на место позади себя, и тут:
	
	* Осторожно проверить, что путь свободен
		~ifd skill_perception >= 20
		~set skill_perception++
	
		[if first]
			И хорошо, что проверил! Потому что дядя Тонин проходит мимо по пути в гарнизон.
			
			Ты сворачиваешься в тени и ждешь, пока он уйдет. Это было близко!
		[else]
			Ты замечаешь свою _маму_, выходящую из гарнизона. Она качает головой от разочарования и бормочет что-то себе под нос.
			
			Ты ждешь немного дольше, прежде чем вылезти и отряхнуться. Хорошо, что проверил сначала!
		[end]
	
	* Выпрыгнуть из трубы
		~setif first ? midleft = tonin : midleft = mom
		~setif first ? skill_stress + 10
		~setif !first ? status = grounded
		~setif !first ? mem_sneakEnd_mom
		
		[if first]
			-и прямо в бочкообразную грудь Главного инспектора Тонина.
			
			"Ну, ну, ну," — говорит он и пытается принять свой самый суровый вид. Не выходит. "Мы вот думали, куда ты пропал, [Name]. Я послал Утопию с дронами-инспекторами искать тебя несколько часов назад. Тебе повезло, что твои родители были слишком заняты, чтобы заметить. Наверное, мне стоит сказать им..."
			
			Ты начинаешь потеть.
			
			"Но, может быть, ты уже усвоил урок," — он посмеивается. Его большая рука хлопает тебя по плечу. "Больше никаких игр за воротами, ладно?"
			
		[else]
			... и прямо в свою Маму.
			
			"[Name]!" — кричит она в ярости. — "Я _знала_, что ты играешь за стенами. Сколько раз мы должны тебе говорить: там снаружи _слишком опасно для детей_!"
			
			Ты начинаешь протестовать, но она поднимает руку. "Ты _наказан_. Немедленно. И я не хочу _никогда_ больше слышать, что ты был там, слышишь меня?"
		[end]
		
		** "Прости..."
			~set skill_rebellion--
			
		** "Да, как скажешь..."
			~set skill_rebellion++
			
			
=== expeditionsUnlock ==============================================
	~if location = none
	
	// ты доказал, что достаточно крепок, чтобы открыть карты исследования
	~set mem_expeditionsUnlock
	~set bg = expeditions
	
	~setif mem_dead_tammy ? left = tonin_angry : left = tonin // не улыбается, если Темми мертва
	~setif mem_chiefSurveyor == utopia ? left = utopia

	// открыть инспектирование и исследование вместо скрытных вылазок после 13-го дня рождения
	~setif mem_couldSurvey ? mem_job_survey = true
	~setif mem_couldSurvey ? mem_job_exploreNearby = true
	~setif mem_couldSurvey ? mem_job_sneak = false
	
	[if mem_chiefSurveyor == tonin]
		[if mem_dead_tammy]
			"Что ж, это так," — отвечает дядя Тонин, его голос тяжел, когда он смотрит на тебя с гордостью. "Ты растешь, да."
			
			Он кивает и похлопывает тебя по плечу, и ты понимаешь, что он думает о Темми.
		[else]
			"Что ж, это так!" — восклицает дядя Тонин, отступая, чтобы с гордостью полюбоваться тобой. "Знаешь, я бы сказал, у тебя, наверное, скачок роста, [Name]. Пусть лучше остерегаются тебя на спортбольном поле!"
		[end]
		
		"Слушай," — говорит он тебе, — "я вижу, что ты крепкий орешек, и я был бы рад видеть тебя в моей команде инспекторов в _будущем_... но тебе придется подождать еще несколько лет. Приходи ко мне, когда подрастешь, ладно, малыш?"
		
	[else]
		"Нууу, _будь я проклята_," — протягивает Утопия, игриво хватая тебя. — "Ты не врешь, малыш: в тебе есть _стойкость_."
		
		[if !mem_job_survey]
			// все еще слишком молод для инспектирования в 12 лет
			"Слушай," — говорит она тебе, — "я поговорю с твоими родителями. Ты еще немного молод, но, держу пари, в _следующем_ году ты сможешь присоединиться к младшим инспекторским командам, ладно? Просто потерпи до тех пор. Прости, росток."
		[else]
			Она просматривает список на своей гололадони. "Ага, похоже, ты одобрен для старта в младших инспекторских командах. И выглядишь так, будто не увянешь от первого укуса хлестательницы. Мы будем держать тебя поближе для начала, здесь и в Прозаичных равнинах. Приходи ко мне, когда будешь готов получить задание."
		[end]
	[end]
	
	*...
		// встреча с Дисом, если все еще слишком молод
		~if !mem_job_survey
		~if !mem_job_sneak
		~set charas = none
		~set speaker = dys
		
		Уныло ты тащишь ноги, возвращаясь через ворота к _безопасности_.
		
		"Псс!"
		
		Что это? Ты оглядываешься вопросительно.
		
		-
		~set left = dys
		
		"Псс!"
		
		Это Дис! Он... прячется за какими-то кустами? Так на него похоже. Он прикладывает палец к губам и жестом зовет тебя за собой. Конечно, решаешь ты, почему бы и нет...
		
		-
		~set bg = walls
		~set left = dys
		
		Он молча ведет тебя вдоль стен к гарнизону, затем приседает в кустах между спортбольным полем и постом охраны, чтобы повозиться с чем-то.
		
		Внезапно его нет.
		
		_Что?!_
		
		-
		~set charas = none
		~set mem_job_sneak = true
		// телепортировать игрока к дренажной трубе, чтобы он знал, где она находится
		// ~call movePlayerTo(sneak) // нет, игроки просто подумают, что это баг, они не поймут
		
		Ты проверяешь, не заметил ли еще кто-нибудь, затем пробираешься туда, где видел его в последний раз. За кустами поп-цветов в стене есть дренажная труба, достаточно большая, чтобы проползти. Тяжелая решетка не дает животным проникнуть внутрь, но она неплотно прилегает. Ее можно сдвинуть в сторону, чтобы пролезть.
		
		Можно _проскользнуть наружу!_
		
		-
		
		Если взрослые не позволяют тебе посетить Долину Головокружения или Подводное Болото, или другие крутые места, куда ходят инспекторы... что ж, можно проскользнуть через сюда, чтобы исследовать окрестности прямо за стенами колонии.
		
		Если Дис делает это, насколько это может быть опасно?
	
	*...
		// ты не можешь исследовать по-настоящему, но уже знаешь, как проскользнуть наружу, напомнить тебе
		~if !mem_job_survey
		~if mem_job_sneak
		
		Уныло ты тащишь ноги, возвращаясь через ворота к _безопасности_.
		
		Что ж, все еще есть секретный путь Диса наружу через дренажную трубу возле гарнизона. Если взрослые пока не позволяют тебе посетить Долину Головокружения или Подводное Болото, или другие крутые места, куда ходят инспекторы... ты все еще можешь проскользнуть через сюда, чтобы исследовать окрестности прямо за стенами колонии.
		
	* Готово
		~if mem_job_survey
		// просто уйти, если получилось

	
/******************************************************************************* }*/
/** Общие квесты на любой местности *********************************************************/
/******************************************************************************* {*/

=== exploreSymMeet ==============================================
	// не скрытная вылазка, так что минимальный возраст 13, на различных картах биомов, кроме охоты или Сияния
	~if job == exploreNearby || job == survey || job == forage || job == artifacts || job == hunt
	// на случайном месте миникбосса, не на персонаже Сим, который появится только после того, как это событие установит mem_met_sym
	~if mapspot = high_miniboss
	// должен быть спасен несколько раз до встречи с ним, или если ты много исследовал
	~if mem_symSaved >= 3 || mem_symSaved >= 1 && mem_work_expeditions >= 15
	// можно встретить Сима в huntUltimate2, в этом случае пропусти это и покажи его на карте
	~if !mem_map_sym
	
	~set sprite = silhouette
	
	~set bg = nearby_close
	
	Тебя несколько раз спасали от неминуемой гибели — или как минимум дискомфорта, — и это не может быть совпадением. Кто-то или что-то проявило к тебе интерес.
	
	За тобой следят.
	
	[if job == artifacts]
		Ты начинаешь чувствовать это место и его опасности, так что в следующий раз, когда услышишь печальный крик пролетающего аэрокальмара, ты идешь на него, а не прочь, высматривая, не наблюдает ли за тобой кто-нибудь со скал.
	[else]
		Ты начинаешь чувствовать это место и его опасности, так что в следующий раз, когда услышишь шипение-щелчок гнезда голодных слизнехватов, ты идешь прямо к нему, высматривая, не наблюдает ли за тобой кто-нибудь из деревьев.
	[end]
	
	-
	
	~set left = stranger_silhouette

	[if job == artifacts]
		Вот! Как раз когда ты думаешь, что придется хватать его щупальца и летать на нем, как на воздушном змее, чтобы заставить своего спасителя появиться, ты видишь тенеобразную человеческую фигуру, возникающую над ближайшим хребтом.
		
		Вы встречаетесь взглядами, и оба замираете. Его миндалевидные глаза расширяются на его не совсем человеческом лице, и он снова скрывается из виду.

	[else]
		Вот! Как раз когда ты думаешь, что придется залезть в это чертово гнездо, чтобы заставить своего спасителя появиться, ты видишь тенеобразную человеческую фигуру, мелькающую за деревом.
		
		Вы встречаетесь взглядами, и оба замираете. Его миндалевидные глаза расширяются на его не совсем человеческом лице, и он скрывается в джунглях.

	[end]
	
	* Позвать Симбиоза
		~if hog_metSym
		~set left = sym_sad // вопросительный
		~set var_knewName
		
		Ты повышаешь голос и называешь его по имени. Мужчина — Симбиоз — ты знаешь его из своих снов о прошлой жизни.
		
		Через несколько секунд Сим появляется снова с подозрительным выражением лица. "Как... ты знаешь мое имя?"
		
		** "Это долгая история."
			> continue2
			
		** "Я узнал его во сне."
			Сим кивает, как будто это имеет для него смысл.
			>! continue2
	
	* Преследовать его!
		~set charas = none
		
		[if job == artifacts]
			Ты мчишься вверх по скале за ним, поднимая много шума, пока загадочная фигура легко перепрыгивает через извивающиеся лианы и движущиеся дюны.
		[else]
			Ты продираешься сквозь подлесок, поднимая много шума, пока загадочная фигура легко прыгает по джунглям прямо впереди тебя, почти не ломая веток.
		[end]
		
		Ты теряешь его из виду...
		
		-
		
		~set effect = screenShake
		~set left = manticore
		~set skill_stress + 20
		
		...и внезапно на тебя нападает мантикора! Должно быть, ее привлек весь этот шум, который ты поднял.
		
		Ты падаешь в грязь в куче дергающихся конечностей и хлещущих лиан, вес верховного хищника выжимает воздух из твоих легких.

		-
		
		~set right = manticore
		~set left = stranger_silhouette
		
		Твой спаситель возвращается как раз перед тем, как мантикора вскроет твой череп, как консервную банку.
		
		Он кричит что-то на незнакомом языке, затем запрыгивает на спину великого хищника. Мантикора ревет и спрыгивает с тебя, сбрасывая незнакомца, пока тот не теряет хватку и с хрустом приземляется в нескольких метрах от тебя. Мантикора исчезает в глуши.
		
		-
		~set charas = none
		
		Мужчина лежит на земле, запыхавшись.
		
		Ты пользуешься возможностью и набрасываешься на него сам. На этот раз он не уйдет!
	
		> pinup
		
	* Выследить его осторожно
		~ifd skill_perception >= 10
		~set charas = none
		
		Вместо того чтобы бросаться за ним, ты используешь свои навыки выслеживания, чтобы осторожно идти по его следу и подкрасться к нему.
		
		Ты набрасываешься, как мантикора, и вы оба падаете на землю. Ты прижимаешь его руки.
		
		-
		= pinup
		~set bg = pinup_sym
		~call cheevo(love_meet_sym)
	
		Ты впервые хорошо видишь его лицо. Длинные, тонкие черты. Странный оттенок его кожи — почти фиолетовый. И его сверкающие, инопланетные глаза. Прежде чем он успеет ответить на твои вопросы, ты спрыгиваешь с него и отступаешь.
		
		[if hog_metSym]
			Погоди... этот мужчина... ты видел его в своих снах. Такое чувство, что ты его знаешь... его имя вертится на кончике памяти, вот-вот вспомнится...
		[else]
			Кто... _что_ он такое?
		[end]
	
		> continue
	
	*= continue
		~set bg = exploreClose
		~set midleft = sym
		
		Он стряхивает пыль со своей одежды и встает во весь рост.
		
		...очень высокий. Тебе приходится сделать еще шаг назад, чтобы как следует его разглядеть. Ты мельком видишь бледно-фиолетовое запястье под его изорванными черными рукавами — хрупкое, как птичьи кости.
		
		"Я _Садовник_," — мурлычет он. — "А ты вторгся на мою планету."
		
		-
		
		[if hog_metSym]
			Ты смотришь на мужчину перед тобой, и тебя пронзает волна воспоминаний. Ты знаешь его... его имя...
		[else]
			Теперь, когда ты видишь его... всего его, ты подтверждаешь, что он определенно не человек. Помимо фиолетового оттенка кожи, он еще и просто... устроен по-другому: человек, но _нет_, и в целом эти маленькие несоответствия складываются в нечто, что определенно... инопланетное.
		[end]
		
		** "Ты инопланетянин?!"
			~if !hog_metSym
			~set hog_metSym
			~set var_firstSym
			~set left = sym_happy
			
			Он смеется, легкий и заразительный смешок, который звучит скорее по-звериному, чем по-человечьи. Это продолжается немного слишком долго. Ты думаешь, он, возможно, подтрунивает над тобой.
		
			"Вообще-то _ты_ здесь инопланетянин, если подумать."
		
			Ты думаешь об этом. Он прав.
		
			> continue2
			
		** "Рад снова тебя видеть, Сим."
			~if hog_metSym
			~set left = sym_sad // любопытный
			~set var_knewName
			
			Сим улыбается тебе, озадаченно. "Я... как ты узнал мое имя?"
			
			*** "Это долгая история."
				> continue2
			
			*** "Я узнал его во сне."
				Сим кивает, как будто это имеет для него смысл.
				>! continue2
		
		**= continue2
			~set left = sym
			
			Он запрыгивает на большой камень и приседает на корточки, задумчиво разглядывая тебя.
			
			"Давай заключим сделку," — говорит он.
			
			-
			
			Он говорит со странной интонацией, делая ударение не на тех слогах, но сами слова даются ему достаточно легко.
			
			"Мне не положено с тобой разговаривать," — говорит он. — "Это против правил. Но раз уж ты меня [поймали|поймала|поймал]... я могу исполнить _одно_ твое желание."
			
			Он хитро улыбается... это шутка?
			
			*** "Я хочу узнать о тебе больше."
				~set left = sym_happy
			
				Он бросает на тебя лукавый взгляд. "Со временем, [Name]. Надеюсь. Мы..."
				
				> continue3
				
			*** "Я хочу тысячу Похвал."
				~set left = sym_happy
			
				"Ого! Я бы с радостью, [Name], обязательно, единственная проблема... что такое Похвала?"
				
				Он снова посмеивается.
				
				"Ты забавно конкретный человек. Я думаю..."
				
				> continue3
			
			*** "Я хочу, чтобы ты перестал надо мной смеяться!"
				~set left = sym_happy
			
				"Да ладно, [Name]," — он посмеивается. — "Мир и так слишком серьезное место. Иногда надо посмеяться над ним, разве нет..."
				
				> continue3
		
	*= continue3
		~set left = sym
		// после того как ты встретил Сима лицом к лицу, он будет появляться в случайных событиях спасения
		~set mem_met_sym = true
		~set mem_map_sym = true // может появиться и на карте
	
		Он делает паузу, и его взгляд на мгновение теряет фокус. Он смотрит прямо перед собой, не моргая.
		
		Затем он медленно потягивается, как кот, и встает. "Что ж, это было весело, но боюсь, долг зовет."
		
		[if var_knewName]
			Он приподнимает воображаемую шляпу в твою сторону. "С нетерпением жду истории о том, как ты узнал мое имя."
		[else]
			Он приподнимает воображаемую шляпу в твою сторону. "Меня, кстати, зовут Симбиоз, или Сим, если хочешь."
		[end]
		
		-
		
		~set charas = none
		
		Он прыгает за камень и исчезает.
		
		Буквально, он исчез. Ты обыскиваешь все вокруг и не можешь найти никаких следов того, куда он мог уйти или как.
		
		-
		
		[if var_firstSym]
			_Звезды_, ты только что встретил _инопланетянина!!_
		[else]
			Тебя охватывает внезапное чувство воспоминания настолько сильное, что ты должен остановиться и схватиться за голову. Встреча с Симом снова... это прямо как раньше.
		[end]


=== expeditionsMutiny ==============================================
	~if job == exploreNearby || job == survey || job == forage || job == artifacts || job == hunt
	~if mapspot = high_start
	~if mem_mutiny
	
	~set sprite = utopia
	
	~set bg = expeditionsReturn_day
		
	Тебе удается стать напарником самого Главного инспектора в твоей следующей экспедиции. Это идеальный шанс поговорить с ней о Губернаторе Люме, но она опережает тебя.
	
	-
	~set bg = explore
	~set left = utopia
	
	"Ух! По этим тропам было легче ходить, когда у нас были ховербайки," — говорит Утопия, взбираясь на особенно трудный участок местности. Она протягивает руку, чтобы помочь тебе подняться. "Эта тварь Люм использует их все для поиска инопланетян, будто это не все равно что искать иголку в стоге сена. Он отправил половину моих команд на болота."
	
	Она указывает на свою пустую кобуру. "_Без_ оружия, могу добавить! Он выжимает из нас все соки, здесь."
	
	-
	
	Она проводит тыльной стороной ладони по губам. "Эта змея в образе человека думает, что все вокруг — это война. Мне тоже не нравится, когда на меня нападают животные, но я определенно скучаю по временам, когда можно было исследовать эту планету, не давая Люму запустить свои лапы в _мое_ подразделение и забирать _мои_ ресурсы для своей чертовой грязной войны."
	
	* "Забавно, что ты упомянула..."
	
		Ты рассказываешь Утопии о плане Марц свергнуть Люма на следующей Вертумналии. Она кажется взволнованной перспективой — ты забываешь, как она молода по сравнению с другими членами Совета.
		
		"Звучит отлично," — говорит она, хлопая тебя по спине. — "Единственный вопрос... кого ты имеешь в виду на замену?"
		
		** "Ну... меня."
			~set var_vote = me
			
			"Тебя?" — протягивает Утопия, затем смеется. — "Прости, не хочу подкалывать, но... не слишком ли ты молод для этой работы?"
			
			Она вытирает глаза, все еще посмеиваясь. "Что ж, мне было всего двадцать два, когда я стала Главным инспектором, полагаю. Это не так уж нереально. Но почему ты? Что ты собираешься делать не так, как эта хвастливая жопа воздухоровы? Уговори меня."
			
			>! continue
		
		** "Марципан."
			~set var_vote = marz
			
			"Марц?" — протягивает Утопия. — "Серьезно? Не слишком ли она... ну. Она молода."
			
			Она скрещивает руки и обдумывает это. "Что ж, мне было всего двадцать два, когда я стала Главным инспектором, полагаю. Это не так уж нереально. Но почему Марц? Что она собирается делать не так, как эта хвастливая жопа воздухоровы? Уговори меня."
			
			>! continue
		
	*= continue
		** "Охота — это опасная трата ресурсов!"
			~ifd mem_work_expeditions > 10
			~if mem_work_hunt == 0 && job != hunt
			~if once
			
			"Чертовски верно," — говорит Утопия, глядя в лес. — "По правде говоря, я просто хочу вернуть свои инспекторские команды. Они там снаружи подвергают себя опасности и будоражат местную фауну. Эти охотники — угроза."
			
			Она поворачивается к тебе. "Так что, ты позаботишься о том, чтобы охоту запретили?"
			
			*** "Да, конечно."
				~set mem_mutinyHuntingBan
				~set mem_mutinyVotes++
				
				[if var_vote = marz]
					Утопия думает об этом, затем кивает. "Ладно," — говорит она, — "тогда у Марц есть мой голос. Удачи, ребята."
				[else]
					Утопия думает об этом, затем кивает. "Ладно," — говорит она, — "тогда у тебя есть мой голос. Удачи, малыш."
					
					Она обнимает тебя за плечо. "Вот совет от одного молодого оленя другому: не позволяй никому говорить тебе, что ты слишком молод," — говорит она. — "Ты живешь на Вертумне так же долго, как и любой из нас."
				[end]
			
			*** "Нет, я не могу этого обещать."
				~set left = utopia_angry
			
				"Кто тебя учил заключать сделки?" — отвечает Утопия, упирая руки в бока. — "Надо что-то дать, чтобы что-то получить, малыш. Это первое правило."
				
				> continue
			
		** "Охота — это опасная трата ресурсов!"
			~ifd mem_work_expeditions > 10
			// mem_work устанавливается после, и ты, возможно, сейчас на охоте
			~if mem_work_hunt > 0 || job == hunt
			~if once
			~set left = utopia_angry
			
			"Ага," — отвечает Утопия, поднимая бровь с недоверием. — "И поэтому я видела тебя там с Вейсом, убивающим все, что движется? Устраивая переполох и подвергая опасности всех моих инспекторов и фуражиров? Ты не проведешь _меня_."
			
			Она цокает языком. "Я лучше буду иметь во главе большого тупого громилу, чем гадюку. Ты так собираешься управлять колонией? Ложью?"
			
			>>>
			
		** "Помнишь, что Люм сделал с твоей _рукой_??"
			~if once
			~set left = utopia_angry
			
			Утопия рефлекторно сжимает ладонь.
			
			"Я не говорила, что он не крысиный ублюдок," — говорит она ровно. — "Я спрашиваю, знаешь ли ты, что делаешь, или это просто игра, в которую вы, дети, играете на этой неделе."
			
			-
			
			Утопия берет тебя за плечи и смотрит прямо в глаза. "Губернатор обвел тех солдат вокруг пальца. И половину моих команд тоже. Он скажет прыгнуть, и гарнизон прыгнет."
			
			"Он велит им прыгнуть _на тебя_, как думаешь, что случится?"
			
			-
			~set left = utopia
			
			"У нас может быть только один шанс скинуть этого сына слизнехвата," — она улыбается, — "так что давай, малыш, _уговаривай меня_."
			
			> continue
		
		** "Ладно, дай я расскажу, что мы сделаем..."
			~call battle(persuasion_easy)
			
			***= win
				// в любом биоме, где ты находишься
				~set bg = exploreClose
				
				[if var_vote = me]
					Ты и Утопия отправляетесь на тяжелый день инспектирования. Пока вы работаете, ты объясняешь свой план и то, как сделаешь все возможное, чтобы защитить Вертумну, как станешь Губернатором.
				[else]
					Ты и Утопия отправляетесь на тяжелый день инспектирования. Пока вы работаете, ты объясняешь свой план и то, как Марц сделает все возможное, чтобы защитить Вертумну, как станет Губернатором.
				[end]
				
				Утопия терпеливо слушает твою речь за ее голос, давая тебе полную возможность объяснить свою позицию. К тому времени, когда второе солнце начинает садиться и пора возвращаться к транспорту, ты приобрел надежного союзника.
				
				-
				~set mem_mutinyVotes++
				~set left = utopia
				
				Утопия обнимает тебя за плечо, пока вы идете обратно. "Вот совет от одного молодого оленя другому: не позволяй никому говорить тебе, что ты слишком молод," — говорит она. — "Ты живешь на Вертумне так же долго, как и любой из нас."
				
			***= lose
				// в любом биоме, где ты находишься
				~set bg = exploreClose
			
				[if var_vote = me]
					Ты и Утопия отправляетесь на тяжелый день инспектирования. Пока вы работаете, ты объясняешь свой план и то, как сделаешь все возможное, чтобы защитить Вертумну, как станешь Губернатором.
				[else]
					Ты и Утопия отправляетесь на тяжелый день инспектирования. Пока вы работаете, ты объясняешь свой план и то, как Марц сделает все возможное, чтобы защитить Вертумну, как станет Губернатором.
				[end]
				
				-
				~set left = utopia_angry
				
				Утопия терпеливо слушает твою речь за ее голос, но... ты чувствуешь, что она не впечатлена. Когда приходит время возвращаться, она качает головой и хлопает тебя по плечу.
				
				[if var_vote = marz]
					"Прости, [Name], я наслушалась. Я хочу, чтобы этот монстр Люм был снят с капитанского мостика не меньше любого, но нам нужно правильное время и правильный кандидат. Этот кандидат — не Марц."
				[else]
					"Прости, [Name], я наслушалась. Я хочу, чтобы этот монстр Люм был снят с капитанского мостика не меньше любого, но нам нужно правильное время и правильный кандидат. Этот кандидат — не ты."
				[end]
				
				-
				
				= fail
				~set bg = command
				~set right = marz
				
				Марц вздыхает, когда ты докладываешь, что не смог заручиться голосом Утопии. "Черт..." — бормочет она, делая заметку на своей гололадони. — "Я была уверена, что мы получим ее. Она что, _забыла_, как он сломал ей руку на глазах у всей колонии? Если даже тот, кто ненавидит его _так сильно_, не будет на нашей стороне..."
				
				Она выглядит разочарованной. "Не знаю, [Name]. Не знаю."
				

=== surveyUnlockForage ==============================================
	~if job == exploreNearby || job == survey || job == forage || job == artifacts || job == hunt
	~if mapspot = start
	// mem_job_forage автоматически открывается в 14-Пыльца-1, но можно начать раньше...
	~if age >= 13
	~if !mem_job_forage
	// это как минимум твой 2-й раз инспектирования
	~if story_surveyStart >= 2
	
	// случайно во время исследования (в первый раз) или в колонии (позже)
	
	~set sprite = utopia
	
	~set bg = expeditionsReturn_day
	~set left = utopia
	
	[if first]
		[if !mem_dead_dys]
			Утопия разбирает вещи, которые принесли фуражиры, раскладывая их по контейнерам для анализа или отправки прямиком на кухни. "Эй, сладкий," — говорит она, даже не глядя вверх. — "Выходишь? Ты с Дисом присматривайте друг за другом там, ладно?"
		[else]
			Утопия разбирает вещи, которые принесли фуражиры, раскладывая их по контейнерам для анализа или отправки прямиком на кухни. "Эй, сладкий," — говорит она, даже не глядя вверх. — "Выходишь? Береги себя там, ладно?"
		[end]
		
		Она вздыхает. "Мне надо разобраться со всем этим до конца дня," — говорит она. — "Это займет у меня целый день... если только ты не хочешь помочь?"
		
	[else]
		// после выбора "Подать заявку на фуражира" позже
		
		Ты находишь Утопию, разбирающую некоторые результаты фуражировки. "Собираешься присоединиться к фуражирам? Честно говоря, помощь не помешала бы. _Если_ у тебя зоркий глаз, конечно."
		
	[end]
	
	* Конечно.
	
		Утопия раскладывает ассортимент грибов. "Ладно, вот простой: какой из этих ребят ядовит?"
	
		** Выбрать правильные
			~ifd skill_perception >= 20
			~set card = edible
			
			Грибы? С этим ты справишься. Ты знаешь, какие грибы подают в кафетерии, и легко выделяешь их из ассортимента.
			
			-
			~set mem_job_forage = true
			
			[if mem_starve]
				"Отличная работа, сладкий!" — восклицает Утопия с широкой улыбкой. — "Боже, я рада... нам _действительно_ не помешало бы больше фуражиров там. Все, что мы сможем добыть, поможет."
			[else]
				"Отличная работа, сладкий!" — восклицает Утопия с широкой улыбкой. — "Ты настоящая свинья — не волнуйся, это комплимент. На Земле свиней использовали, чтобы вынюхивать закопанные грибы. Иди туда и накопай нам немного, а?"
			[end]
				
			"Закончи, что делал в инспекторской команде, но возвращайся в следующем месяце, и я определю тебя к фуражирам," — говорит она. — "Ты будешь в Долине Головокружения. Это одно из лучших мест для сбора диких ягод и других съедобных растений."
			
		** Выбрать наугад
		
			Ты выбираешь, с переменным успехом.
			
			"Ты просто угадываешь, да," — говорит Утопия с усмешкой. — "Ничего. Отправляйся туда с инспекторской командой и смотри в оба. Ты быстро научишься!"
			
	* "Не, не стоит."
	
		Утопия пожимает плечами. "Ладно," — говорит она. — "Но знаешь... я ищу новых фуражиров. Дай знать, если захочешь подать заявку позже."
	
	
=== surveyUnlockArtifactsStart ==============================================
	~if job == exploreNearby || job == survey || job == forage || job == artifacts || job == hunt
	~if mapspot = start
	~if age >= 15
	// продолжай повторяться в дикой природе, пока не найдешь и не наберешь достаточно восприятия
	~if !mem_job_artifacts
	~if !mem_dead_dys
	~if repeat
	
	~set sprite = utopia

	~set bg = plains_close
	~set left = utopia
	~set right = dys
	~set speaker = utopia

	Главный инспектор Утопия останавливает тебя и Диса возле станции. "Чувствуешь?" — спрашивает она.

	Ты _чувствуешь_ что-то вроде зловещего отдаленного стука, пульсирующего сквозь землю. Если стоять неподвижно, можно почувствовать, как вибрация поднимается и в твое собственное тело, словно массивный глобальный пульс.

	[if mem_thumper]
		Должно быть, где-то рядом топтун.
	[end]
	
	-
	~setif first ? speaker = utopia : speaker = dys
	
	[if first]
		"Хм," — говорит Утопия, проверяя показания на своей гололадони.
		
		"Полагаю, это естественного происхождения," — заключает она. — "Но от этого все равно мурашки по коже. Вам двоим лучше это проверить. Только держитесь вместе и снимайте показания по пути, на случай, если это опасно."
		
		Ты с Дисом смотрите друг на друга, оживленно.
	[else]
		Дис выглядит оживленным. "На этот раз мы точно выясним, что это такое."
		
		Утопия машет вам обоим, сосредоточившись на своих показаниях.

	[end]
	
	-
	~set bg = plains
	~set right = dys
	
	Вы следуете за стуком — теперь уже слышимым шумом, становящимся громче — на запад, пока не триангулировали его местоположение. Вы знаете, что близко, но звук будто идет со всех сторон, и ваши приборы недостаточно точны, чтобы помочь.
	
	* "Как думаешь, что это??"
		~if first
	
		Дис разводит руками. "Инопланетяне," — говорит он серьезно.
		
		[if story_dysSym]
			Вы оба смеетесь. "Нет, то есть, наверное, это что-то связанное с Садовниками, да?" — говорит он. — "Должно быть."
		[elseif mem_met_sym]
			Тебе интересно, знает ли он, насколько он прав... это ведь _точно_ как-то связано с Симом, да?
		[end]
	
		>>>
	
	* Следовать за звуком
		~ifd skill_perception >= 30
		~call battle(perception_15)

	* Сдаться
		= lose
		~set skill_stress + 10
		// можно попробовать позже, не сразу, потому что keep и мультибиомные события не смешиваются
		
		Этот звук идет _отовсюду_. Ты понятия не имеешь.
		
		Дис расстроен. "Да ладно, это же должно быть _где-то_!" — кричит он, но планета не выдает своих секретов.
		
	*= win
		~set bg = plains_close
		~set charas = none
		~set left = thumper
		
		~set card = thumper
		~set mem_thumper
	
		Ты находишь... машину? _Древнюю_, сломанную машину, похожую на маленькое пирамидальное строение из какого-то темного камня или кости, которое выглядит почти так, будто оно так выросло.
		
		-
		
		За исключением того... что она не сломана. Она просто очень старая и заросшая, больше похожа на груду щебня и корней, чем на здание или двигатель, или что бы это ни было. Тем не менее, изнутри доносится стучащий звук. Вибрации здесь сильные, вызывают странное ощущение в животе.
		
		-
		
		Сначала ты не замечаешь, но от машины идет любопытная тропа: линия шириной в полметра, где земля немного более протоптана и гола. Она уходит по прямой на запад. Или... идет с запада.
		
		Животные могли сделать эту тропу? Или сам топтун?
		
		** Проанализировать топтун
			~ifd skill_engineering >= 20
			~set skill_kudos + 10
			~set skill_engineering++
			
			Это определенно _не_ естественное образование. На некоторых частях топтуна можно разглядеть то, что когда-то могло быть письменами, глубоко вырезанными в не-совсем-камне. Этому, должно быть, _тысячи_ лет. Та его часть, что все еще работает, находится глубоко в его сердцевине.
			
			У него еще и странный запах, сладкий, как что-то забродившее. Потоки воздуха выходят из отверстий у вершины в такт стуку.
			
			-
			
			Ты также замечаешь, что животные здесь _гораздо_ агрессивнее, чем обычно. Даже насекомые жужжат над головой назойливее. Твои сканеры регистрируют более высокий уровень атакующих феромонов.
			
			Что бы ни происходило, фауне это тоже не нравится.
			
			> utopia
			
		** Восхититься им
			~set skill_engineering++
			
			Ты проводишь рукой по его изрытой, вибрирующей поверхности. Как что-то невероятно старое может все еще работать? Это определенно не естественное образование — ты даже можешь увидеть что-то вроде письмен, глубоко вырезанных в не-совсем-камне.
			
			Дис начинает под нос издавать звуки _унц-унц-унц_ в такт топтуну.
			
			Довольно забористый ритм.
			
			> utopia
		
	*= utopia
		~set bg = expeditionsReturn_day
		~set right = dys
		
		В конце концов, вы возвращаетесь, чтобы поделиться находками с Утопией.
		
		"_Точно_ инопланетная цивилизация," — настаивает Дис.
		
		-
		~set left = utopia
		
		Утопия почесывает затылок. "Полагаю... _возможно_," — говорит она. — "Вы сказали, оно выглядит древним, да? Возможно, здесь _могла_ быть другая цивилизация давным-давно. Мы находили и другие признаки... но ничего _активного_, как это."
		
		-
		~set mem_job_artifacts = true
		
		"Кажется, оно указывает на запад, или получает что-то оттуда..." Она щелкает пальцами. "Там есть несколько весьма неприятных линий хребтов, которые мешали нам проверить это направление, но... у нас есть планеры! И мотивация! Это половина дела!"
		
		Она делает заметку в своей гололадони. "Я открою Западное Нагорье для инспекторов," — говорит она, затем ухмыляется. — "Ух! Попробуй произнести это трижды быстро!"
		
		** "Дис прав — это точно инопланетяне."
			~set speaker = dys

			[if story_dysSym]
				Дис бросает на тебя многозначительный взгляд, но ничего не говорит.
			[elseif mem_met_sym]	
				Дис торжественно кивает. Хотел бы ты рассказать ему о Симе.
			[else]		
				Дис торжественно кивает.
			[end]
		
		** "Там нам придется держаться вместе."
			~set right = dys_sad // mid=surprised, adult=eyes down
			~set speaker = dys
		
			Дис с любопытством смотрит на тебя. "Конечно, если боишься," — говорит он.
					
		** "Если я что-нибудь найду, Похвалы мои."
			~set right = dys_angry
			~set speaker = dys
		
			Дис закатывает на тебя глаза. "Заткнись со своими дурацкими Похвалами," — бормочет он.
	
	
=== exploreUnlockHunt ==============================================
	~if job == exploreNearby || job == survey || job == forage || job == artifacts || job == hunt
	~if mapspot = start
	~if !mem_job_hunt
	~if mem_war
	
	// первый раз на карте, после этого из раздела экспедиций
	
	// можно установить в false, если хочешь быть пацифистом
	~set mem_jobready_hunt = true
	
	~set sprite = utopia
	
	~setif first ? bg = expeditionsReturn_day : bg = expeditions
	~set left = utopia
	~setif first ? midleft = rhett
	~setif first ? right = vace
	~set speaker = utopia
	
	[if first]
		Ты приходишь в палатку Экспедиций и находишь там не только Утопию, но и Начальника охраны Ретта и Вейса.
		
		"Я просто не могу выделить еще своих людей," — объясняет Утопия им. — "Вы уже забрали почти всех фуражиров, и у нас уже работают скелетные экипажи на Нагорье."
	
	[else]
		Утопия вздыхает, когда ты говоришь ей, что ищешь Ретта.
		
		"Что ж, я не могу тебя остановить, сладкий. Ретт в Гарнизоне, хотя. Мне не нравится, как он ходит сюда и пытается переманить моих людей."
	[end]
	
	-
	= decision
	
	~setif !first ? bg = garrison
	~setif !first ? left = rhett
	~setif !first ? midleft = vace
	~set speaker = rhett
	
	[if first]
		"Есть один человек, которого ты можешь выделить," — отвечает Ретт, поворачиваясь к тебе лицом. — "Привет, [Name]... как насчет перевестись в одну из моих охотничьих групп? Утопия говорит, ты один из ее лучших исследователей. Такой человек нам пригодится."
		
		Он указывает на Вейса. "Мне нужен кто-то, кто сможет угнаться за этим." Вейс горделиво выпячивает грудь.
	[else]
		Ты направляешься в Гарнизон.
		
		"А, это ты снова," — говорит Ретт. — "Собираешься попробовать попасть в охотничью группу опять?"
		
		Вейс смотрит на тебя из-под лба, где он чистит плазменную винтовку, и хмурится.
	[end]		
	
	* "Мой окончательный ответ — нет!"
		~if !first
		// убирает подачу заявки с главного экрана экспедиций
		~set mem_jobready_hunt = false
		~set speaker = vace
		
		Вейс фыркает. "Чего еще ожидать от ребенка из колонии," — бормочет он. — "Слишком трусливы, чтобы встать и защитить свою колонию."
		
		-
		
		Ретт даже не моргает. Хотя он Страто, как и ты, он даже не пытается защитить твой выбор.
		
		"Эта планета нуждается во всех типах," — напоминает Ретт Вейсу, затем поворачивается к тебе.
		
		"Как знаешь, [Name]. Я всегда говорил, что колония должна играть более активную роль в самозащите, но я не могу тебя заставить. Пока что."
		
	* "Я никогда не убил бы живое существо!"
		~if first
		~set speaker = vace
		
		Вейс фыркает. "Даже если бы оно пыталось убить тебя? Я знал, что вы, дети Страто, мягкие... не знал, что вы еще и самоубийцы."
		
		-
		
		~set speaker = rhett
		
		"Не все созданы для ситуаций жизни и смерти," — говорит Ретт, затем поворачивается к тебе.
		
		"Знаешь, [Name], дело не только в убийстве," — продолжает он. — "Иногда это выслеживание и идентификация опасных существ, прежде чем они достигнут наших стен. Тебе не обязательно придется их убивать."
		
		Он поднимает руку. "Но тебе не нужно решать прямо сейчас. Поговори с Утопией позже, если передумаешь."
	
	* "Зачем нам их убивать?"
		~if once
		~set speaker = vace
		
		Вейс смеется. "Потому что они убивают _нас_, [Name], не отставай."
		
		Он сжимает кулак. "Это классическая борьба между человечеством и природой," — говорит он. — "Природа — это не только цветы и радуги — на Земле _всё_ и _все_ пытались убить друг друга. Ради выживания, ради еды... черт, даже просто ради _забавы_."
		
		"Не могу дождаться, когда подстрелю настоящий трофейный ксен," — он посмеивается, обрамляя руками воображаемую картину на стене. — "Первого десятиочкового оленя на Вертумне."
		
		** "Я не хочу убивать животных ради забавы!"
			~set speaker = rhett
		
			Ретт хмурится на Вейса. "Ты не будешь убивать их ради забавы. Ты будешь убивать их, чтобы защитить колонию."
			
			Вейс закатывает глаза.
			
			>> decision
			
		** "Звучит отлично!"
		
			> yes
	
	* "Конечно, с радостью поохочусь!"
		= yes
		
		~setif mem_xeno_vriki ? var_seenVriki
		~setif !first ? mem_xeno_vriki++
		~set charas = none
		~set left = rhett
		~set midleft = vace
		~set speaker = rhett
		
		Ретт кивает. "Хорошо. Рад это слышать," — говорит он, затем поворачивается к Вейсу. — "Но сначала мне нужно убедиться, что ты знаешь основы. Вейс, принеси врики."
		
		-
		
		~set charas = none
		~set midright = vriki
		~set speaker = rhett

		[if var_seenVriki]
			Вейс уходит в палатку и возвращается с врики на длинной цепи. Она бойкая и с ясными глазами, некоторые могли бы даже сказать _милая_, и достает тебе почти до пояса.

			Вейс выглядит гордым своей обязанностью носителя монстра.
		[else]
			Вейс уходит в палатку и выводит врики на длинной цепи. Это розовое, паукообразное существо, достающее тебе почти до пояса. Каждая пара ног специализирована для разных задач: хватающие пальцы, сегментированные прыгающие ноги, щупальца и так далее.
			
			Оно моргает своими многочисленными глазами на тебя и издает стрекочущий звук. Некоторые могли бы назвать это существо _милым_.
		[end]
		
		-
		
		"Я поймал этого сам," — говорит тебе Ретт. — "Я добавляю его в учебный зоопарк, так что не убивай его, но можешь дать пару тычков. Затем скажи мне: какие у него слабости? Сильные стороны? Как бы ты победил его в бою?"
		
		** Проанализировать зверя
			~ifd skill_animals >= 30
		
			~call battle(animals, winAnimals, loseAnimals)
		
		** Атаковать его
			~ifd skill_combat >= 30
		
			~call battle(combat, winCombat, loseCombat)
		
		** Сделать предположение наугад
			> loseAnimals
	
	*= winAnimals
		Ты долго изучаешь существо. Оно выглядит нервным, бегает из стороны в сторону и издает щелкающий звук. Ты бросаешь в него палку, и оно подпрыгивает и хватает ее своими челюстями. Оно сердито разрывает ее пополам, затем смотрит на тебя.
		
		[if call_hasPet(vriki)]
			Ты думаешь о своем собственном питомце, [=mem_vrikiName]. Этот крупнее и больше похож на гриб, возможно, даже близок к концу подвижной части своего жизненного цикла. Ты знаешь, что однажды все врики должны укорениться и стать материнскими "деревьями" для следующего поколения.
			
			Ты перечисляешь другие факты, которые узнал, играя с [=mem_vrikiName], и Ретт кивает.
			
		[else if var_seenVriki]
			Ты проводишь еще несколько тестов и думаешь о других случаях, когда видел врики в дикой природе. Это одно из многих существ на Вертумне, которые, кажется, больше гриб, чем животное. Ты видел их самых разных размеров: от маленьких бегунов размером с твою ладонь до прыгающих хищников и возвышающихся материнских "деревьев", укорененных на одном месте.
			
			Этот довольно крупный, так что, вероятно, он раздражен тем, что его вытащили из дикой природы на этой стадии жизненного цикла. Ему, наверное, скоро пора укореняться.
			
		[else]
			Ты никогда не видел врики вживую, но они довольно хорошо задокументированы в голоархивах. Это одно из многих существ на Вертумне, которые, кажется, больше гриб, чем животное. Ты видел их самых разных размеров: от маленьких бегунов размером с твою ладонь до прыгающих хищников и возвышающихся материнских "деревьев", укорененных на одном месте.
			
			Ты перечисляешь некоторые факты, которые помнишь из своих занятий, и Ретт кивает.
		[end]
		
		-
		~set skill_animals++
		~set mem_job_hunt = true
		~set charas = none
		~set left = rhett
		
		"Хорошо," — говорит Ретт. — "Посмотрим, насколько ты хорош в охоте на них, как в их определении. Присоединяйся к охотничьей группе в следующем месяце, и посмотрим, как ты справишься на Болотах."

	*= winCombat
	
		[if call_hasPet(vriki)]
			Ты думаешь о том, как ты играл в борьбу со своим собственным питомцем, [=mem_vrikiName]. Этот врики больше и намного злее, так что придется быть осторожным.
			
		[else]
			Его было бы легко застрелить, но Ретт сказал не убивать, так что ты приближаешься к Врики без оружия. Она смотрит на тебя нервно, массивные челюсти щелкают, тонкая слюна капает с них. Ты надеешься, что она не ядовита, но даже в этом случае укус вырвет из тебя большой кусок.
		
		[end]
		
		Ты делаешь финт влево, затем двигаешься вправо и хватаешь цепь существа, дергая ее изо всех сил. Когда врики спотыкается, ты ныряешь за нее и накидываешь петлю цепи ей на шею.
		
		-
		~set charas = none
		~set left = rhett
		~set skill_combat++
		~set mem_job_hunt = true
		
		"Достаточно!" — останавливает тебя Ретт. — "Отлично сработано, [Name]. Похоже, ты именно тот, кто нам нужен. Присоединяйся к охотничьей группе в следующем месяце, и посмотрим, как ты справишься на Болотах."
	
	*= loseAnimals
		
		Его маленькие бусинки-глаза недоверчиво моргают на тебя, когда оно вздрагивает от твоего прикосновения.
		
		Ты изучаешь его какое-то время, но его секреты остаются тебе неизвестны.
		
		> fail
	
	*= loseCombat
	
		Ты берешь палку и топаешь к тупому существу, чтобы избить его.
		
		Врики подпрыгивает и хватает палку своими мощными челюстями, разламывая ее пополам. Затем он прыгает тебе на грудь, с силой бросая тебя на спину и выбивая из тебя воздух.
		
		-
		~set charas = none
		~set right = vace
		
		Вейс дергает за цепь существа, стаскивая его с тебя с визгом как раз перед тем, как эти челюсти впиваются тебе в горло.
		
		>! fail
			
	*= fail
		~set charas = none
		~set right = vace
		~set skill_stress + 5
		
		"Какой идиот," — бормочет Вейс себе под нос, качая головой. — "Ты даже с врики не справишься."
		
		-
		
		~set left = rhett
		
		Ретт вздыхает. "Полагаю, ты не так крепок, как я думал," — говорит он. — "Может, если ты потренируешься, придешь на занятия по защите и поизучаешь этих зверей, ты будешь готов. Поговори с Утопией снова, когда будешь."
		
		-
		
		~set speaker = vace
		
		Вейс закатывает глаза.
		
		"Да, отжимания и учеба," — он усмехается. — "_Вот что_ поможет [нему|ей|ему]."

	
/******************************************************************************* }*/
/** Общие коллекционные предметы ********************************************************/
/******************************************************************************* {*/

// эти заполняют mapSpotType = Collectible места и появляются только как текст над головой игрока

=== exploreCollectibleWood ==============================================
	~if mapspot = collectible_wood
	~if repeat
	
	~set sprite = wood
	~set card = wood
	~set mem_foundCollectible
	
	// переносы строк, чтобы красиво обернуть текст в маленьком окне
	
	Ты добываешь прочное
	Бревно грибодрева
		
		
=== exploreCollectibleFruit ==============================================
	~if mapspot = collectible_fruit
	~if repeat
	
	~set sprite = fruit
	~set card = fruit
	~set mem_foundCollectible
	
	Ты собираешь спелый
	Синий поплавник
		
		
=== exploreCollectibleEgg ==============================================
	~if mapspot = collectible_egg
	~if repeat
	
	~set sprite = egg
	~set card = egg
	~set mem_foundCollectible
	
	Ты находишь увесистое
	Красное ксено-яйцо
		
		
=== exploreCollectibleCrystal ==============================================
	~if mapspot = collectible_crystal
	~if repeat
	
	~set sprite = crystal
	~set card = crystal
	~set mem_foundCollectible
	
	Ты находишь сверкающий
	Кристальный кластер
		
		
=== exploreCollectibleRoots ==============================================
	~if mapspot = collectible_roots
	~if repeat
	
	~set sprite = roots
	~set card = roots
	~set mem_foundCollectible
	
	Ты выкапываешь пучок
	Лекарственных корней
		
		
=== exploreCollectibleFlower ==============================================
	~if mapspot = collectible_yellowFlower
	~if repeat
	
	~set sprite = yellowFlower
	~set card = yellowFlower
	~set mem_foundCollectible
	
	Ты осторожно срываешь
	Жёлтый цветок
		
		
=== exploreCollectibleDevice ==============================================
	~if mapspot = collectible_device
	~if repeat
	
	~set sprite = device
	~set card = device
	~set mem_foundCollectible
	
	Что это за
	Странное устройство

